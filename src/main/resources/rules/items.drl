
package rules

import com.sbz.models.Receipt;
import com.sbz.models.Item;
import com.sbz.models.ItemDiscount;
import com.sbz.models.Article;
import com.sbz.models.Sale;
import java.time.ZonedDateTime;
import java.util.Date;
import java.time.Instant;
import java.util.ArrayList;

rule "osnovni popust 10% za stavku sa više od 20 artikla, koji ne pripadaju kategoriji siroke potrosnje"
	lock-on-active true
	when
			Receipt($items: items)
		$i: Item(amount>20, article.category.id != "category 1") from $items
	then
		System.out.println($i.getArticle().getName()+": osnovni popust 10%");
		$i.getItemDiscounts().add(new ItemDiscount($i, 10, "BASE"));		
end

rule "osnovni popust 5% za stavku sa više od 5 artikla, koji pripadaju kategoriji televizora, racunara ili laptopova"
	lock-on-active true
	when
			Receipt($items: items)
		$i: Item(amount>5, article.category.id in ("category 10", "category 9", "category 12")) from $items
	then
		System.out.println($i.getArticle().getName()+": osnovni popust 5%");
		$i.getItemDiscounts().add(new ItemDiscount($i, 5, "BASE"));		
end

rule "osnovni popust 7% za stavku cija ukupna narucena vrednost prelazi 5000, i njen artikal pripada sirokoj potrosnji"
	lock-on-active true
	when
			Receipt($items: items)
		$i: Item(totalPrice>5000, article.category.id == "category 1") from $items
	then
		System.out.println($i.getArticle().getName()+": osnovni popust 7%");
		$i.getItemDiscounts().add(new ItemDiscount($i, 7, "BASE"));		
end

rule "dodatni popust 2% za stavku ukoliko je artikal iz stavke vec kupovan u prethodnih 15 dana"
	lock-on-active true
	when
		$r:	Receipt($items: items, $cr: customer.getReceipts())
		    Receipt(state=="SUCCESS", $previousItems: items, date.toInstant().isAfter(ZonedDateTime.now().plusDays(-15).toInstant())) from $cr
	   $pi: Item() from $previousItems
		$i:	Item(article.id==$pi.article.id, $id: itemDiscounts) from $items
			not (ItemDiscount(type=="ADDITIONAL", discount==2) from $id)
	then
		$i.getItemDiscounts().add(new ItemDiscount($i, 2, "ADDITIONAL"));
		update($r);
		System.out.println($i.getArticle().getName()+": dodatni popust 2%");	
end

rule "dodatni popust 1% za stavku ukoliko su proizvodi iz kategorije tog artikla kupovani u prethodnih 30 dana"
	lock-on-active true
	when
		$r:	Receipt($items: items, $cr: customer.getReceipts())
		    Receipt(state=="SUCCESS", $previousItems: items, date.toInstant().isAfter(ZonedDateTime.now().plusDays(-30).toInstant())) from $cr
	   $pi: Item() from $previousItems
		$i:	Item(article.category==$pi.article.category, $id: itemDiscounts) from $items
			not (ItemDiscount(type=="ADDITIONAL", discount==1) from $id)
	then
		$i.getItemDiscounts().add(new ItemDiscount($i, 1, "ADDITIONAL"));
		update($r);
		System.out.println($i.getArticle().getName()+": dodatni popust 1%");	
end

rule "akcijski popust ukoliko je artikal iz stavke na akciji"
	lock-on-active true
	when
		$r:	Receipt($items: items, $d: date)
		$i:	Item($s: article.category.sales) from $items
		    Sale(fromDate.getTime()< (new Date()).getTime(), toDate.getTime()> (new Date()).getTime(), $discount: discount) from $s
	then
		$i.getItemDiscounts().add(new ItemDiscount($i, $discount, "ADDITIONAL"));
		System.out.println($i.getArticle().getName()+": akcijski popust");	
end